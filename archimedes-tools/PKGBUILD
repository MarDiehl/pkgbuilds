# Maintainer: eolianoe <eolianoe At GoogleMAIL DoT com>
# Maintainer: A Austin <acm.austin at gmail.com>
# Contributor: Martin Ortbauer <mortbauer@gmail.com>

pkgbase=archimedes-tools
pkgname=('triangle' 'showme')
pkgver=1.6
pkgrel=9
arch=('i686' 'x86_64')
url='http://www.cs.cmu.edu/~quake/archimedes.html'
pkgdesc='Archimedes is a set of tools, including mesh generators, for performing unstructured finite element simulations'
license=('custom')
source=('http://www.netlib.org/voronoi/triangle.zip'
        'triangle_definitions.patch'
        'showme_functions.patch'
        'triangle_functions.patch')
md5sums=('10aff8d7950f5e0e2fb6dd2e340be2c9'
         '98844d1b1d9de73101b660412132cd07'
         'ec7def5c6a10d2e854440ab0a221c0b9'
         '749da38851cac18494ec7cc2b56c6a19')
makedepends=('libx11')

prepare() {
  patch < ${srcdir}/triangle_definitions.patch triangle.h
  patch < ${srcdir}/triangle_functions.patch triangle.c
  patch < ${srcdir}/showme_functions.patch showme.c
}

build(){
  gcc ${CFLAGS} -DANSI_DECLARATORS -o triangle triangle.c -lm
  gcc ${CFLAGS} -DANSI_DECLARATORS -DTRILIBRARY -Wall -fpic -c triangle.c
  gcc ${CFLAGS} -shared -o libtriangle.so triangle.o

  gcc ${CFLAGS} -DANSI_DECLARATORS -o showme showme.c -lX11
}

package_triangle() {
  url='http://www.cs.cmu.edu/~quake/triangle.html'
  pkgdesc='A Two-Dimensional Quality Mesh Generator and Delaunay Triangulator.'
  depends=('glibc')

  install -Dm755 triangle "${pkgdir}/usr/bin/triangle"
  install -Dm644 triangle.h "${pkgdir}/usr/include/triangle.h"
  install -Dm644 libtriangle.so "${pkgdir}/usr/lib/libtriangle.so"
  install -Dm644 "${srcdir}/README" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_showme() {
  url='http://www.cs.cmu.edu/~quake/showme.html'
  pkgdesc='Show Me graphically displays the contents of geometric files, especially those generated by Triangle'
  depends=('libx11')

  install -Dm755 showme "${pkgdir}/usr/bin/showme"
  install -Dm644 "${srcdir}/README" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
